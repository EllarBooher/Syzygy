#version 460
#extension GL_EXT_buffer_reference2 : require
#extension GL_ARB_shading_language_include : require

#include "../types/camera.glsl"
#include "common.glinl"

// This shader produces the actual output of our atmosphere volumetric pipeline

// Perpsective projection of a map parameterized by
// u := azimuth from -pi to pi
// v := elevation from -pi/2 to pi/2

layout(local_size_x = 16, local_size_y = 16) in;
layout(rgba16, set = 0, binding = 0) uniform image2D image;
//layout(set = 1, binding = 0) uniform sampler2D fragmentDepth;

layout(set = 0, binding = 1) uniform sampler2D skyview_LUT;
layout(set = 0, binding = 2) uniform sampler2D transmittance_LUT;

layout(buffer_reference, std430) readonly buffer AtmosphereBuffer
{
    Atmosphere atmospheres[];
};

layout(buffer_reference, std430) readonly buffer CameraBuffer
{
    Camera cameras[];
};

layout(push_constant) uniform PushConstant
{
    AtmosphereBuffer atmosphereBuffer;
    CameraBuffer cameraBuffer;

    uint atmosphereIndex;
    uint cameraIndex;

    uvec2 drawExtent;
} pc;

vec3 sampleMap_Direction(const Atmosphere atmosphere, const vec3 position, const vec3 direction)
{
    const vec3 normalized = normalize(direction);

    // Horizon zenith cannot be less than PI/2, so we use sin and assume it is a quadrant 2 angle
    const float sinHorizonZenith = atmosphere.planetRadiusMm / length(position);
    const float horizonZenith = PI - asin(sinHorizonZenith);

    const float cosViewZenith = normalized.y;
    const float cosHorizonZenith = -safeSqrt(1.0 - sinHorizonZenith * sinHorizonZenith);

    const float viewZenith = acos(normalized.y);

    // We still want uv.y = 0 and uv.y = 1 to the extreme zenith angles
    // But since we make the horizon a straight line through the middle, and its zenith may not be PI/2, 
    // we must scale angles differently depending on if they are above or below the horizon.

    float u, v;

    if (cosViewZenith > cosHorizonZenith)
    {
        // Above horizon, v shall range from 0.0 to 0.5
        // viewZenith varies from 0 to horizonZenith

        const float angleFraction = viewZenith / horizonZenith;

        // Increase angle density towards v = 0.5
        v = (1.0 - sqrt(1.0 - angleFraction)) * 0.5;
    }
    else
    {   
        // Below horizon, v shall range from 0.5 to 1
        // viewZenith varies from horizonZenith to PI

        const float angleFraction = (viewZenith - horizonZenith) / (PI - horizonZenith);

        // Increase angle density towards v = 0.5
        v = sqrt(angleFraction) * 0.5 + 0.5;
    }

    {
        // Sun is always positioned at uv = 0

        const vec2 projectedLightDirection = normalize(vec2(-atmosphere.incidentDirectionSun.x, -atmosphere.incidentDirectionSun.z));
        const vec2 projectedViewDirection = normalize(vec2(direction.x, direction.z));
        
        u = clamp(dot(projectedLightDirection,projectedViewDirection), -1.0, 1.0) * 0.5 + 0.5;
    }

    return texture(skyview_LUT, vec2(u, v)).rgb;
}

vec3 sampleSunDisk(const Atmosphere atmosphere, const vec3 position, const vec3 direction)
{
    const vec3 directionToSun = -atmosphere.incidentDirectionSun;

    const float cosDirectionSun = dot(direction, directionToSun) / (length(direction) * length(directionToSun));

    const float sinSunRadius = atmosphere.sunAngularRadius;
    const float sinDirectionSun = safeSqrt(1.0 - cosDirectionSun * cosDirectionSun);

    const vec3 transmittanceToSun = sampleTransmittanceLUT_Ray(transmittance_LUT, atmosphere, position, direction);

    if (cosDirectionSun < 0.0)
    {
        return vec3(0.0);
    }

    return transmittanceToSun * (1.0 - smoothstep(0.2 * sinSunRadius, sinSunRadius, sinDirectionSun));
}

vec3 sampleGround(const Atmosphere atmosphere, const vec3 origin, const vec3 direction)
{
    // Assumes the ray hits the ground

    // TODO: Figure out what to do when inside the planet
    float planet_t0, planet_t1;
    const bool hitPlanet =
        raySphereIntersection(origin, direction, atmosphere.planetRadiusMm, planet_t0, planet_t1) && planet_t0 > 0.0;
    
    if (!hitPlanet)
    {
        return vec3(0.0);
    }

    const vec3 surfacePosition = origin + planet_t0 * direction;
    const vec3 surfaceNormal = normalize(surfacePosition);

    const vec3 lightDirection = -atmosphere.incidentDirectionSun;
    const vec3 viewDirection = -direction;
    // Quick and dirty estimate of surface albedo
    const vec3 halfwayDirection = normalize(lightDirection + viewDirection);
    const float specularPower = 160.0;
    const float microfacetAttenuation = pow(clamp(dot(halfwayDirection, surfaceNormal), 0.0, 1.0), specularPower);
    const float normalizationTerm = (specularPower + 2) / 8.0;
    const vec3 specular = vec3(normalizationTerm * microfacetAttenuation);
    const vec3 diffuse = vec3(0.4) / PI;

    const vec3 fresnel =
        vec3(0.04) + (1.0 - vec3(0.04)) * pow(1.0 - clamp(dot(halfwayDirection, lightDirection), 0.0, 1.0), 5);
    const vec3 albedo = mix(diffuse, specular, fresnel);

    vec3 transmittanceToSun = sampleTransmittanceLUT_Ray(transmittance_LUT, atmosphere, surfacePosition, lightDirection);

    const vec3 surfaceLuminance = transmittanceToSun * albedo * clamp(dot(surfaceNormal, lightDirection),0.0,1.0);
    const vec3 transmittanceToSurface = sampleTransmittanceLUT_Segment(transmittance_LUT, atmosphere, origin, surfacePosition);

    const vec3 aerielPerspectiveLuminance = computeLuminanceScatteringIntegral(atmosphere, transmittance_LUT, origin, direction, planet_t0);

    return surfaceLuminance * transmittanceToSurface + aerielPerspectiveLuminance;
}

void main()
{
    const vec2 size = imageSize(image);
    const ivec2 texelCoord = ivec2(gl_GlobalInvocationID.xy);
    if (texelCoord.x >= size.x || texelCoord.y >= size.y)
    {
        return;
    }

    const Camera camera = pc.cameraBuffer.cameras[pc.cameraIndex];
    const Atmosphere atmosphere = pc.atmosphereBuffer.atmospheres[pc.atmosphereIndex];

    // Convert from engine position (in meters with -y up) 
    // and the skyview coordinates (in megameters with +y up)
    vec3 position = camera.position.xyz / METERS_PER_MM;
    position.y *= -1;
    position.y += atmosphere.planetRadiusMm;

    const vec2 clipSpaceUV = vec2((gl_GlobalInvocationID.xy / vec2(pc.drawExtent)) - 0.5) * 2.0;
    const float nearPlaneDepth = 1.0;
    const vec4 directionViewSpace = camera.inverseProjection * vec4(clipSpaceUV, nearPlaneDepth, 1.0);
    vec3 direction = normalize((camera.rotation * directionViewSpace).xyz);
    direction.y *= -1;

    const float radius = length(position);
    const float sin_horizonZenith = atmosphere.planetRadiusMm / radius;
    const float cos_horizonZenith = -safeSqrt(1.0 - sin_horizonZenith * sin_horizonZenith);

    vec3 sunIlluminanceToSkyLuminanceTransfer = vec3(0.0);
    
    // If sun is above horizon, the sun disk and sky is visible. 
    // We use the precomputed SkyView LUT to get this value.
    // The sky has low visual frequency, so it is more efficient to look up the values from a low resolution texture
    // than it is to raymarch for each pixel.
    const float cos_directionZenith = dot(direction, position) / (length(direction) * length(position));
    if (cos_directionZenith < cos_horizonZenith)
    {   
        sunIlluminanceToSkyLuminanceTransfer += sampleGround(atmosphere, position, direction);
    }
    else
    {
        sunIlluminanceToSkyLuminanceTransfer += sampleMap_Direction(atmosphere, position, direction);
        sunIlluminanceToSkyLuminanceTransfer += sampleSunDisk(atmosphere, position, direction);
    }

    vec3 luminance = sunIlluminanceToSkyLuminanceTransfer * atmosphere.sunIntensitySpectrum;

    // ad-hoc tonemapping
    const vec3 color = pow(luminance * 10.0, vec3(1.2));

    imageStore(image, texelCoord, vec4(color, 1.0));
}